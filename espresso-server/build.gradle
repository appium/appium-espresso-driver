// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.getIntegerProperty = { String propertyName, int defaultValue ->
        Object propertyValue = project.getProperties().get(propertyName)
        (propertyValue) ? Integer.parseInt(propertyValue.toString(), 10) : defaultValue
    }
    ext.getStringProperty = { String propertyName, String defaultValue ->
        Object propertyValue = project.getProperties().get(propertyName)
        (propertyValue) ? propertyValue.toString() : defaultValue
    }
    ext.getBooleanProperty = { String propertyName, boolean defaultValue ->
        Object propertyValue = project.getProperties().get(propertyName)
        (propertyValue) ? Boolean.parseBoolean(propertyValue) : defaultValue
    }

    ext.android_gradle_plugin_version = getStringProperty('appiumAndroidGradlePlugin', '4.1.1')
    ext.kotlin_version = getStringProperty('appiumKotlin', '1.3.72')

    repositories {
        maven {
            url "https://maven.google.com"
        }
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.android.tools.build:gradle:$android_gradle_plugin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        maven { url 'https://maven.google.com' }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
